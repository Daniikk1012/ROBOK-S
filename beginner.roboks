>> Beginner's method algorithm

x2 >> Look at yellow

>> Do a white cross around yellow center
(
  { [UR = D] => U }3
  {
    [FR = D] => R
    [BR = D] => R'
    [DR = D] => R2
    [FL = D] => d2R'
    [BL = D] => d2R
    [DL = D] => D2R2
    [RF = D] => dR'
    [RB = D] => d'R
    [LF = D] => dR
    [LB = D] => d'R'
    [DF = D] => DR2
    [DB = D] => D'R2
    [RU = D] => Rd'R
    [RD = D] => RdR'
    [LD = D] => DF'RF
    [FD = D] => F'RF
    [BD = D] => BR'B'
  }
)4

>> Now solve the proper white cross
(
  { [RU <> R | UR <> D] => U }3
  R2 d
)4

>> Cross is done, now corners
<sexy move>: RUR'U'
(
  { [RUF <> D, URF <> D, FRU <> D] => U }3
  { [RUF <> D, URF <> D, FRU <> D] => (
    {
      [DFR = D, FDR = F, RDF = R | DFR <> D, FDR <> D, RDF <> D] => d
    }3
    <sexy move>
  )}
  {
    [RUF = D] => { [URF <> R | FRU <> F] => d }
    [URF = D] => { [RUF <> F | FRU <> R] => d }
    [FRU = D] => { [RUF <> R | URF <> F] => d }
  }3
  { [DFR <> D | FDR <> F | RDF <> R] => <sexy move> }5
)4

>> First layer is done. Now the second layer
<put piece left>: U' F' U F <sexy move>'
(
  { [RU = U | UR = U] => U }3
  { [RU = U | UR = U] => (
    { [FR = F, RF = R | FR = U | RF = U] => d }3
    <put piece left>
    U2
  )}
  { [RU <> R] => d }3
  {
    [RU = R, UR = F] => <put piece left>
    [RU = R, UR = B] => U B U' B' U' R' U R
  }
)4

>> First two layers done, now yellow cross
{
  [UL = U, UB = U, UF <> U, UR <> U] => F <sexy move>' F'
  [UL = U, UR = U, UB <> U, UF <> U] => F <sexy move> F'
  [UL <> U, UR <> U, UB <> U, UF <> U] => F <sexy move> F' U2 F <sexy move>' F'
  [UL = U, UR = U, UB = U, UF = U] => ()
  [] => U
}4
<permute cross>: R U R' U R U2 R'
{
  [FU = F, LU <> L, BU <> B, RU <> R] => ()
  [FU <> F, LU = L, BU <> B, RU <> R] => y'
  [FU <> F, LU <> L, BU = B, RU <> R] => y2
  [FU <> F, LU <> L, BU <> B, RU = R] => y
  [FU = F, LU <> L, BU = B, RU <> R] => <permute cross> U' y'
  [FU <> F, LU = L, BU <> B, RU = R] => <permute cross> U y
  [FU = F, LU = L, BU = B, RU = R] => ()
  [] => U
}3
{ [RU <> R] => <permute cross> }2

>> Yellow cross done. Now rotate the corners
x2 >> We do that upside down
(
  { [DFR <> D] => <sexy move>2 }3
  D
)4
x2 >> Return to normal orientation

>> Lastly, permute the corners
<swap>: R' D2 R
(
  { [FRU = FU, RFU = RU] => U }3
  <swap>
  { [LBD <> RU | DLB <> FU] => U }3
  <swap>
  { [FRU <> D, RFU <> D, URF <> D] => U }3
  <swap>
)2
{ [RU <> R] => U }3
